plugins {
    id 'java'
    // Download task
    id "de.undercouch.download" version "5.6.0"
}

/**
 * Project Properties
 */
sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion
ext {
	buildID = System.getenv( 'BUILD_ID' ) ?: '0'
	branch = System.getenv( 'BRANCH' ) ?: 'development'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
	// Until BoxLang is published to Maven Central
	// Look for it in the local build directory
	// You must run `./gradle build -x test` in the BoxLang project
    compileOnly files( 'src/test/resources/libs/boxlang-aws-lambda-1.0.0-all.jar' )

    // Testing Dependencies
	testImplementation files( 'src/test/resources/libs/boxlang-aws-lambda-1.0.0-all.jar' )
	testImplementation 'com.amazonaws:aws-lambda-java-core:1.2.3'
    testImplementation 'org.slf4j:slf4j-nop:2.0.6'
    testImplementation "org.junit.jupiter:junit-jupiter:5.+"
	testImplementation "org.mockito:mockito-core:5.+"
	testImplementation "com.google.truth:truth:1.+"
}

java {
}

compileJava {
	// Compiler Options
	options.incremental = true
	options.encoding = 'UTF-8'
	options.debug()
}

compileTestJava {
	dependsOn( 'downloadBoxLang' )
}

test {
    useJUnitPlatform()
    testLogging {
		showStandardStreams = true
	}
}

/**
 * Build the Lambda zip file for deployment on AWS
 */
task buildMainZip( type: Zip ) {
	dependsOn( 'downloadBoxLang' )
	archiveFileName = "${project.name}-${version}.zip"
	from 'src/main/bx/Lambda.bx'
	into( 'lib' ) {
		// All the Jars in the src/test/resources/libs directory
		from 'src/test/resources/libs'
	}
}
build.finalizedBy( buildMainZip )

/**
 * TEMPORARY until we publish to maven
 * Task to download the latest jar from https://ortus-temp.s3.amazonaws.com/jericho/libs/boxlang-1.0.0-all.jar
 */
task downloadBoxLang( type: Download ) {
	dependsOn( 'processTestResources' )
	// Create the destination directory
	doFirst {
		file( "src/test/resources/libs" ).mkdirs()
	}

    // Configure the URL of the file to download
	src "https://s3.amazonaws.com/downloads.ortussolutions.com/ortussolutions/boxlang-runtimes/boxlang-aws-lambda/${boxlangVersion}/boxlang-aws-lambda-${boxlangVersion}-all.jar"

	// Specify the destination directory for the downloaded file
    dest "src/test/resources/libs/boxlang-aws-lambda-${boxlangVersion}-all.jar"
    overwrite true
    onlyIfModified false
}
